plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
    id 'io.github.gradle-nexus.publish-plugin' version '1.1.0'
    id 'pl.allegro.tech.build.axion-release' version '1.13.2'
}

group = 'io.github.lsd-consulting'
version = scmVersion.version

sourceCompatibility = '11'

repositories {
    mavenCentral()
}

dependencies {
    implementation 'io.github.lsd-consulting:lsd-core:0.1.18'
    implementation 'org.junit.jupiter:junit-jupiter-api:5.5.2'
    implementation 'org.junit.jupiter:junit-jupiter-engine:5.5.2'
    implementation 'com.j2html:j2html:1.4.0'

    testImplementation 'org.assertj:assertj-core:3.15.0'
    testImplementation 'com.approvaltests:approvaltests:11.4.0'
    testImplementation 'org.mockito:mockito-all:1.10.19'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.8.0-M1'
}

test {
    useJUnitPlatform()
    testLogging {
        events "failed"
        exceptionFormat "full"
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar) {
    archiveClassifier = 'javadoc'
    from javadoc
}

artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = "$group"
            artifactId = 'lsd-junit5'
            version = "${scmVersion.version}"

            from components.java
            artifact sourcesJar
            artifact javadocJar
            pom {
                name = 'lsd-junit5'
                description = 'A library providing a Junit5 extension for generating LSD reports'
                url = 'https://github.com/lsd-consulting/lsd-junit5.git'
                licenses {
                    license {
                        name = "MIT License"
                        url = "https://github.com/lsd-consulting/lsd-junit5/blob/main/LICENSE"
                        distribution = "repo"
                    }
                }
                developers {
                    developer {
                        name = "Nick"
                        email = "nicholas.mcdowall@gmail.com"
                        organization = 'NKM IT Solutions'
                        organizationUrl = 'https://github.com/nickmcdowall'
                    }
                    developer {
                        name = "Lukasz"
                        email = "lukasz.gryzbon@gmail.com"
                        organization = 'Integreety Ltd.'
                        organizationUrl = 'https://github.com/integreety'
                    }
                }
                scm {
                    url = "https://github.com/lsd-consulting/lsd-junit5.git"
                }
            }
            repositories {
                maven {
                    name = 'sonatype'
                    url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
                    credentials(PasswordCredentials)
                }
            }
        }
    }
}

nexusPublishing {
    repositories {
        sonatype {
            nexusUrl.set(uri("https://s01.oss.sonatype.org/service/local/"))
            snapshotRepositoryUrl.set(uri("https://s01.oss.sonatype.org/content/repositories/snapshots/"))
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}
