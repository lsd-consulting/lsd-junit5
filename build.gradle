plugins {
    id 'org.jetbrains.kotlin.jvm' version '2.2.20'
    id("org.jetbrains.dokka-javadoc") version "2.1.0"
    id 'maven-publish'
    id 'signing'
    id 'io.github.gradle-nexus.publish-plugin' version '2.0.0'
    id 'com.palantir.git-version' version '4.0.0'
    id 'jacoco'
}

apply from: 'hooks.gradle'

group = 'io.github.lsd-consulting'
version = gitVersion().replaceAll("^v", "")

repositories {
    mavenLocal()
    mavenCentral()
}

kotlin {
    jvmToolchain(17)
}

dependencies {
    // Import JUnit BOM to manage all JUnit-related dependency versions
    implementation platform('org.junit:junit-bom:6.0.0')
    
    api 'io.github.lsd-consulting:lsd-core:7.2.1'
    implementation 'org.junit.jupiter:junit-jupiter-api'
    implementation 'org.junit.jupiter:junit-jupiter-engine'
    implementation 'org.junit.platform:junit-platform-launcher'
    implementation 'org.junit.platform:junit-platform-engine'

    testImplementation 'org.assertj:assertj-core:3.27.6'
    testImplementation 'com.approvaltests:approvaltests:25.7.0'
    testImplementation 'org.mockito:mockito-core:5.20.0'
    testImplementation 'org.junit.jupiter:junit-jupiter-params'
}

jacocoTestReport {
    reports {
        xml.required = true
        html.required = true
        html.outputLocation = layout.buildDirectory.dir("reports/coverage")
    }
}

test {
    useJUnitPlatform()
    testLogging {
        events "failed"
        exceptionFormat "full"
    }
    finalizedBy jacocoTestReport
}

java {
    withSourcesJar()
    withJavadocJar()
}

// Configure the javadoc task to use Dokka
javadocJar {
    dependsOn dokkaGeneratePublicationJavadoc
    from layout.buildDirectory.dir('dokka/javadoc')
}

artifacts {
    archives jar
    archives sourcesJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = "$group"
            artifactId = 'lsd-junit5'
            version = "${version}"

            from components.kotlin
            artifact sourcesJar
            artifact javadocJar
            pom {
                name = 'lsd-junit5'
                description = 'A library providing a Junit5 extension for generating LSD reports'
                url = 'https://github.com/lsd-consulting/lsd-junit5.git'
                licenses {
                    license {
                        name = "MIT License"
                        url = "https://github.com/lsd-consulting/lsd-junit5/blob/main/LICENSE"
                        distribution = "repo"
                    }
                }
                developers {
                    developer {
                        name = "Nick"
                        email = "nicholas.mcdowall@gmail.com"
                        organization = 'NKM IT Solutions'
                        organizationUrl = 'https://github.com/nickmcdowall'
                    }
                    developer {
                        name = "Lukasz"
                        email = "lukasz.gryzbon@gmail.com"
                        organization = 'Integreety Ltd.'
                        organizationUrl = 'https://github.com/integreety'
                    }
                }
                scm {
                    url = "https://github.com/lsd-consulting/lsd-junit5.git"
                }
            }
            repositories {
                maven {
                    name = 'ossrh-staging-api'
                    url = "https://ossrh-staging-api.central.sonatype.com/service/local/staging/deploy/maven2/"
                    credentials(PasswordCredentials)
                }
            }
        }
    }
}

nexusPublishing {
    repositories {
        sonatype {
            nexusUrl.set(uri("https://ossrh-staging-api.central.sonatype.com/service/local/"))
            snapshotRepositoryUrl.set(uri("https://central.sonatype.com/repository/maven-snapshots/"))
        }
    }
}

signing {
    if (project.findProperty("signingKey")) {
        // Use in-memory ascii-armored keys
        def signingKey = findProperty("signingKey")
        def signingPassword = findProperty("signingPassword")
        useInMemoryPgpKeys(signingKey, signingPassword)
        sign publishing.publications.mavenJava
    } else {
        // Use signing properties in ~/.gradle/gradle.properties
        sign publishing.publications.mavenJava
    }
}
